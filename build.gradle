plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "maven.modrinth:bbs-mod:${project.bbs_version}-${project.minecraft_version}"
    //modImplementation files("libs/bbs-${project.bbs_version}-${project.minecraft_version}.jar")

    include modImplementation("com.caoccao.javet:javet:${javet_version}")
    include modImplementation("com.caoccao.javet:javet-v8-windows-x86_64:${javet_version}")
    //include modImplementation("com.caoccao.javet:javet-v8-linux-x86_64:${javet_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.register("scriptingJavadocsJson", Exec) {
    executable "javadoc"
    args "-doclet", "com.theuran.doclets.JsonDoclet",
            "-docletpath", "./run/jsonDoclet.jar",
            "-sourcepath", "./src/main/java",
            "-classpath", sourceSets.main.compileClasspath.asPath,
            "-subpackages", "com.theuran.mappet.api.scripts.code"

    doLast {
        file("src/main/resources/assets/mappet/docs.json").setText(file("docs.json").text);
    }
}

tasks.processResources.dependsOn("scriptingJavadocsJson")